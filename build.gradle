plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.14.0'
    id 'jacoco'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.8'
}

group = 'ca.cutterslade.gradle'
version = '1.6.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.maven.shared:maven-dependency-analyzer:1.11.3'

    testImplementation('org.spockframework:spock-core:2.0-M5-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
}

tasks.named('publishPlugins') {
    it.dependsOn check
}

gradlePlugin {
    plugins {
        dependencyAnalyze {
            id = 'ca.cutterslade.analyze'
            displayName = 'Gradle Dependency Analyze'
            description = 'Dependency analysis plugin for gradle. This plugin attempts to replicate the functionality of the maven dependency plugin\'s analyze goals which fail the build if dependencies are declared but not used or used but not declared.'
            implementationClass = 'ca.cutterslade.gradle.analyze.AnalyzeDependenciesPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gradle-dependency-analyze/gradle-dependency-analyze'
    vcsUrl = 'https://github.com/gradle-dependency-analyze/gradle-dependency-analyze'
    tags = ['dependency', 'verification', 'analyze']
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.enabled true
        csv.enabled true
    }
}

jacoco {
    toolVersion = '0.8.6'

}

tasks.named('wrapper') {
    gradleVersion '7.0'
    distributionType = Wrapper.DistributionType.ALL
}
